START:
CLS
PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
PRINT"                                    ,------*------,"
PRINT"                    ,-------------   '---  ------'"
PRINT"                     '-------- --'      / /"
PRINT"                         ,---' '-------/ /--,"
PRINT"                          '----------------'":PRINT
PRINT"                    THE HMSS FUMBLENUTS --- NCAA-1201A"
PRINT:PRINT:PRINT:PRINT:PRINT
270 Z$="                         "
330 DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
370 T=INT(RND(1)*20+20)*100:T0=T:T9=25+INT(RND(1)*10):D0=0:E=3000:E0=E
440 P=10:P0=P:S9=200:S=0:B9=2:K9=0:X$="":X0$=" IS "
470 DEF FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
475 DEF FNR(R)=INT(RND(R)*7.98+1.01)
480 REM INITIALIZE ENTERPRIZE'S POSITION
490 Q1=FNR(1):Q2=FNR(1):S1=FNR(1):S2=FNR(1)
530 FORI=1TO9:C(I,1)=0:C(I,2)=0:NEXTI
540 C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
600 C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
670 FORI=1TO8:D(I)=0:NEXTI
710 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"
810 REM SETUP WHAT EXHISTS IN GALAXY . . .
815 REM K3= # KLINGONS  B3= # STARBASES  S3 = # STARS
820 FORI=1TO8:FORJ=1TO8:K3=0:Z(I,J)=0:R1=RND(1)
850 IFR1>.98THENK3=3:K9=K9+3:GOTO LINE980
860 IFR1>.95THENK3=2:K9=K9+2:GOTO LINE980
870 IFR1>.80THENK3=1:K9=K9+1

LINE980:
B3=0:IF RND(1)>.96THEN B3=1:B9=B9+1

1040 G(I,J)=K3*100+B3*10+FNR(1):NEXT J:NEXT I:IF K9>T9 THEN T9=K9+1
1100 IF B9<>0 THEN GOTO LINE1200
1150 IF G(Q1,Q2)<200THEN G(Q1,Q2)=G(Q1,Q2)+120:K9=K9+1
1160 B9=1:G(Q1,Q2)=G(Q1,Q2)+10:Q1=FNR(1):Q2=FNR(1)
LINE1200:
1200 K7=K9:IF B9<>1THEN X$="S":X0$=" ARE "
1230 PRINT"YOUR ORDERS ARE AS FOLLOWS:"
1240 PRINT"     DESTROY THE";K9;"KLINGON WARSHIPS WHICH HAVE INVADED"
1252 PRINT"   THE GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS"
1260 PRINT"   ON STARDATE";T0+T9;"  THIS GIVES YOU";T9;"DAYS.  THERE";X0$
1272 PRINT"  ";B9;"STARBASE";X$;" IN THE GALAXY FOR RESUPPLYING YOUR SHIP"
1280 PRINT:REM PRINT"HIT ANY KEY EXCEPT RETURN WHEN READY TO ACCEPT COMMAND"
LINE1300:
1300 I=RND(1):REM IF INP(1)=13 THEN GOTO LINE1300

LINE1320:
REM HERE ANY TIME NEW QUADRANT ENTERED
1320 Z4=Q1:Z5=Q2:K3=0:B3=0:S3=0:G5=0:D4=.5*RND(1):Z(Q1,Q2)=G(Q1,Q2)
1390 IF Q1<1 OR Q1>8 OR Q2<1 OR Q2>8 THEN GOTO LINE1600
1430 GOSUB LINE9030:PRINT:IF T0<>T THEN GOTO LINE1490
1460 PRINT"YOUR MISSION BEGINS WITH YOUR STARSHIP LOCATED"
1470 PRINT"IN THE GALACTIC QUADRANT, '";G2$;"'.":GOTO LINE1500
LINE1490:
1490 PRINT"NOW ENTERING ";G2$;" QUADRANT . . ."

LINE1500:
PRINT:K3=INT(G(Q1,Q2)*.01):B3=INT(G(Q1,Q2)*.1)-10*K3

1540 S3=G(Q1,Q2)-100*K3-10*B3:IF K3=0 THEN GOTO LINE1590
1560 PRINT"COMBAT AREA      CONDITION RED":IF S>200 THEN GOTO LINE1590
1580 PRINT"   SHIELDS DANGEROUSLY LOW"
LINE1590:
1590 FOR I=1 TO 3:K(I,1)=0:K(I,2)=0:NEXT I

LINE1600:
1600 FORI=1TO3:K(I,3)=0:NEXTI:Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
1660 REM POSITION FUMBLENUTS IN QUADRANT, THEN PLACE "K3" KLINGONS, &
1670 REM "B3" STARBASES, & "S3" STARS ELSEWHERE.
1680 A$="<*>":Z1=S1:Z2=S2:GOSUB LINE8670:IFK3<1THEN GOTO LINE1820
1720 FOR I=1 TO K3:GOSUB LINE8590:A$="+K+":Z1=R1:Z2=R2
1780 GOSUB LINE8670:K(I,1)=R1:K(I,2)=R2:K(I,3)=S9*(0.5+RND(1)):NEXT I
LINE1820:
1820 IF B3<1 THEN GOTO LINE1910
1880 GOSUB LINE8590:A$=">!<":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB LINE8670
LINE1910:
1910 FOR I=1 TO S3:GOSUB LINE8590:A$=" * ":Z1=R1:Z2=R2:GOSUB LINE8670:NEXT I

LINE1980:
1980 GOSUB LINE6430

LINE1990:
IF S+E > 10 THEN IF E > 10 OR D(7) = 0 THEN GOTO LINE2060

2020 PRINT:PRINT"** FATAL ERROR **   YOU'VE JUST STRANDED YOUR SHIP IN "
2030 PRINT"SPACE":PRINT"YOU HAVE INSUFFICIENT MANEUVERING ENERGY,";
2040 PRINT" AND SHIELD CONTROL":PRINT"IS PRESENTLY INCAPABLE OF CROSS";
2050 PRINT"-CIRCUITING TO ENGINE ROOM!!":GOTO LINE6220

LINE2060:
2060 INPUT"COMMAND";A$
2080 FOR I=1 TO 9:IF LEFT$(A$,3) <> MID$(A1$,3*I-2,3)THEN GOTO LINE2160
2140 ON I GOTO LINE2300,LINE1980,LINE4000,LINE4260,LINE4700,LINE5530,LINE5690,LINE7290,LINE6270

LINE2160:
2160 NEXTI:PRINT"ENTER ONE OF THE FOLLOWING:"
2180 PRINT"  NAV  (TO SET COURSE)"
2190 PRINT"  SRS  (FOR SHORT RANGE SENSOR SCAN)"
2200 PRINT"  LRS  (FOR LONG RANGE SENSOR SCAN)"
2210 PRINT"  PHA  (TO FIRE PHASERS)"
2220 PRINT"  TOR  (TO FIRE PHOTON TORPEDOES)"
2230 PRINT"  SHE  (TO RAISE OR LOWER SHIELDS)"
2240 PRINT"  DAM  (FOR DAMAGE CONTROL REPORTS)"
2250 PRINT"  COM  (TO CALL ON LIBRARY-COMPUTER)"
2260 PRINT"  XXX  (TO RESIGN YOUR COMMAND)":PRINT:GOTO LINE1990

LINE2300:
REM COURSE CONTROL BEGINS HERE
2300 INPUT"COURSE (0-9)";C1:IFC1=9THENC1=1
2310 IF C1>=1 AND C1<9 THEN GOTO LINE2350
2330 PRINT"   LT. SULU REPORTS, 'INCORRECT COURSE DATA, SIR!'":GOTO LINE1990
LINE2350:
2350 X$="8":IFD(1)<0THENX$="0.2"
2360 PRINT"WARP FACTOR (0-";X$;")";:INPUT W1:IF D(1)<0 AND W1>.2 THEN GOTO LINE2470
2380 IF W1>0 AND W1<=8 THEN GOTO LINE2490
2390 IF W1=0 THEN GOTO LINE1990
2420 PRINT"   CHIEF ENGINEER SCOTT REPORTS 'THE ENGINES WON'T TAKE";
2430 PRINT" WARP ";W1;"!'":GOTO LINE1990
LINE2470:
2470 PRINT"WARP ENGINES ARE DAMAGED.  MAXIUM SPEED = WARP 0.2":GOTO LINE1990
LINE2490:
2490 N=INT(W1*8+.5):IF E-N>=0 THEN GOTO LINE2590
2500 PRINT"ENGINEERING REPORTS   'INSUFFICIENT ENERGY AVAILABLE"
2510 PRINT"                       FOR MANEUVERING AT WARP";W1;"!'"
2530 IFS<N-EORD(7)<0THEN GOTO LINE1990
2550 PRINT"DEFLECTOR CONTROL ROOM ACKNOWLEDGES";S;"UNITS OF ENERGY"
2560 PRINT"                         PRESENTLY DEPLOYED TO SHIELDS."
2570 GOTO LINE1990

LINE2590:
REM KLINGONS MOVE/FIRE ON MOVING STARSHIP . . .
2590 FORI=1TOK3:IFK(I,3)=0THEN 2700
2610 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB LINE8670:GOSUB LINE8590
2660 K(I,1)=Z1:K(I,2)=Z2:A$="+K+":GOSUB LINE8670
2700 NEXT I:GOSUB LINE6000:D1=0:D6=W1:IF W1>=1 THEN D6=1
2770 FOR I=1 TO 8:IF D(I)>=0 THEN GOTO LINE2880
2790 D(I)=D(I)+D6:IFD(I)>-.1 AND D(I)<0 THEN D(I)=-.1:GOTO LINE2880
2800 IF D(I)<0THEN GOTO LINE2880
2810 IF D1<>1 THEN D1=1:PRINT"DAMAGE CONTROL REPORT:  ";
2840 PRINTTAB(8);:R1=I:GOSUB LINE8790:PRINTG2$;" REPAIR COMPLETED."

LINE2880:
2880 NEXT I:IF RND(1)>.2THEN GOTO LINE3070
2910 R1 = FNR(1):IF RND(1)>=.6 THEN GOTO LINE3000
2930 D(R1)=D(R1)-(RND(1)*5+1):PRINT"DAMAGE CONTROL REPORT:  ";
2960 GOSUB LINE8790:PRINTG2$;" DAMAGED":PRINT:GOTO LINE3070
LINE3000:
3000 D(R1)=D(R1)+RND(1)*3+1:PRINT"DAMAGE CONTROL REPORT:  ";
3030 GOSUB LINE8790:PRINTG2$;" STATE OF REPAIR IMPROVED":PRINT

LINE3070:
REM BEGIN MOVING STARSHIP
3070 A$="   ":Z1=INT(S1):Z2=INT(S2):GOSUB LINE8670
3110 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):X=S1:Y=S2
3140 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):Q4=Q1:Q5=Q2
3170 FOR I=1 TO N:S1=S1+X1:S2=S2+X2:IF S1<1 OR S1>=9 OR S2<1 OR S2>=9THEN GOTO LINE3500
3240 S8=INT(S1)*24+INT(S2)*3-26:IFMID$(Q$,S8,2)="  "THEN GOTO LINE3360
3320 S1=INT(S1-X1):S2=INT(S2-X2):PRINT"WARP ENGINES SHUT DOWN AT ";
3350 PRINT"SECTOR";S1;",";S2;"DUE TO BAD NAVAGATION":GOTO LINE3370
LINE3360:
3360 NEXT I:S1=INT(S1):S2=INT(S2)

LINE3370:
3370 A$="<*>":Z1=INT(S1):Z2=INT(S2):GOSUB LINE8670:GOSUB LINE3910:T8=1
3430 IF W1<1 THEN T8=.1*INT(10*W1)
3450 T=T+T8:IFT>T0+T9THEN GOTO LINE6220
3470 REM SEE IF DOCKED, THEN GET COMMAND
3480 GOTO LINE1980

LINE3500:
REM EXCEEDED QUADRANT LIMITS
3500 X=8*Q1+X+N*X1:Y=8*Q2+Y+N*X2:Q1=INT(X/8):Q2=INT(Y/8):S1=INT(X-Q1*8)
3550 S2=INT(Y-Q2*8):IFS1=0THENQ1=Q1-1:S1=8
3590 IFS2=0THENQ2=Q2-1:S2=8
3620 X5=0:IFQ1<1THENX5=1:Q1=1:S1=1
3670 IF Q1>8THENX5=1:Q1=8:S1=8
3710 IF Q2<1THENX5=1:Q2=1:S2=1
3750 IF Q2>8THENX5=1:Q2=8:S2=8
3790 IF X5=0 THEN GOTO LINE3860
3800 PRINT"LT. UHURA REPORTS MESSAGE FROM STARFLEET COMMAND:"
3810 PRINT"  'PERMISSION TO ATTEMPT CROSSING OF GALACTIC PERIMETER"
3820 PRINT"  IS HEREBY *DENIED*.  SHUT DOWN YOUR ENGINES.'"
3830 PRINT"CHIEF ENGINEER SCOTT REPORTS  'WARP ENGINES SHUT DOWN"
3840 PRINT"  AT SECTOR";S1;",";S2;"OF QUADRANT";Q1;",";Q2;".'"
3850 IF T>T0+T9 THEN GOTO LINE6220
LINE3860:
3860 IF 8*Q1+Q2=8*Q4+Q5 THEN GOTO LINE3370
3870 T=T+1:GOSUB LINE3910:GOTO LINE1320
3900 REM MANEUVER ENERGY S/R **
LINE3910:
3910 E=E-N-10:IF E>=0 THEN RETURN
3930 PRINT"SHIELD CONTROL SUPPLIES ENERGY TO COMPLETE THE MANEUVER."
3940 S=S+E:E=0:IF S<=0 THEN S=0
3980 RETURN


LINE4000:
3990 REM LONG RANGE SENSOR SCAN CODE
4000 IFD(3)<0THENPRINT"LONG RANGE SENSORS ARE INOPERABLE":GOTO LINE1990
4030 PRINT"LONG RANGE SCAN FOR QUADRANT";Q1;",";Q2
4040 O1$="-------------------":PRINTO1$
4060 FORI=Q1-1TOQ1+1:N(1)=-1:N(2)=-2:N(3)=-3:FORJ=Q2-1TOQ2+1
4120 IFI>0ANDI<9ANDJ>0ANDJ<9THENN(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
4180 NEXTJ:FORL=1TO3:PRINT": ";:IFN(L)<0THENPRINT"*** ";:GOTO LINE4230
4210 PRINTRIGHT$(STR$(N(L)+1000),3);" ";
LINE4230:
4230 NEXT L:PRINT":":PRINTO1$:NEXT I:GOTO LINE1990

LINE4260:
REM PHASER CONTROL CODE BEGINS HERE
4260 IFD(4)<0THENPRINT"PHASERS INOPERATIVE":GOTO LINE1990
4265 IF K3>0 THEN LINE4330
LINE4270:
4270 PRINT"SCIENCE OFFICER SPOCK REPORTS  'SENSORS SHOW NO ENEMY SHIPS"
4280 PRINT"                                IN THIS QUADRANT'":GOTO LINE1990
LINE4330:
4330 IF D(8)<0 THEN PRINT"COMPUTER FAILURE HAMPERS ACCURACY"
4350 PRINT"PHASERS LOCKED ON TARGET;  ";
LINE4360:
4360 PRINT"ENERGY AVAILABLE =";E;"UNITS"
4370 INPUT"NUMBER OF UNITS TO FIRE";X:IF X<=0 THEN GOTO LINE1990
4400 IF E-X<0 THEN GOTO LINE4360
4410 E=E-X:IF D(7)<0 THEN X=X*RND(1)
4450 H1=INT(X/K3):FOR I=1 TO 3:IF K(I,3)<=0THEN GOTO LINE4670
4480 H=INT((H1/FND(0))*(RND(1)+2)):IF H>.15*K(I,3) THEN GOTO LINE4530
4500 PRINT"SENSORS SHOW NO DAMAGE TO ENEMY AT ";K(I,1);",";K(I,2):GOTO LINE4670
LINE4530:
4530 K(I,3)=K(I,3)-H:PRINTH;"UNIT HIT ON KLINGON AT SECTOR";K(I,1);",";
4550 PRINT K(I,2):IFK(I,3)<=0THEN PRINT"*** KLINGON DESTROYED ***":GOTO LINE4580
4560 PRINT"   (SENSORS SHOW";K(I,3);"UNITS REMAINING)":GOTO LINE4670
LINE4580:
4580 K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB LINE8670
4650 K(I,3)=0:G(Q1,Q2)=G(Q1,Q2)-100:Z(Q1,Q2)=G(Q1,Q2):IFK9<=0THEN GOTO LINE6370
LINE4670:
4670 NEXT I:GOSUB LINE6000:GOTO LINE1990

LINE4700:
REM PHOTON TORPEDO CODE BEGINS HERE
4700 IF P<=0 THEN PRINT"ALL PHOTON TORPEDOES EXPENDED":GOTO LINE1990
4730 IF D(5)<0THEN PRINT"PHOTON TUBES ARE NOT OPERATIONAL":GOTO LINE1990
LINE4760:
4760 INPUT"PHOTON TORPEDO COURSE (1-9)";C1:IF C1=9 THEN C1=1
4780 IF C1>=1 AND C1<9 THEN GOTO LINE4850
4790 PRINT"ENSIGN CHEKOV REPORTS,  'INCORRECT COURSE DATA, SIR!'"
4800 GOTO LINE1990
LINE4850:
4850 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):E=E-2:P=P-1
4860 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=S1:Y=S2
4910 PRINT"TORPEDO TRACK:"
LINE4920:
4920 X=X+X1:Y=Y+X2:X3=INT(X+.5):Y3=INT(Y+.5)
4960 IF X3<1 OR X3>8 OR Y3<1 OR Y3>8 THEN GOTO LINE5490
5000 PRINT"               ";X3;",";Y3:A$="   ":Z1=X:Z2=Y:GOSUB LINE8830
5050 IF Z3<>0 THEN GOTO LINE4920
5060 A$="+K+":Z1=X:Z2=Y:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE5210
5110 PRINT"*** KLINGON DESTROYED ***":K3=K3-1:K9=K9-1:IF K9<=0 THEN GOTO LINE6370
5150 FOR I=1 TO 3:IF X3=K(I,1) AND Y3=K(I,2)THEN GOTO LINE5190
5180 NEXT I:I=3
LINE5190:
5190 K(I,3)=0:GOTO LINE5430
LINE5210:
5210 A$=" * ":Z1=X:Z2=Y:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE5280
5260 PRINT "STAR AT";X3;",";Y3;"ABSORBED TORPEDO ENERGY.":GOSUB LINE6000:GOTO LINE1990
LINE5280:
5280 A$=">!<":Z1=X:Z2=Y:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE4760
5330 PRINT"*** STARBASE DESTROYED ***":B3=B3-1:B9=B9-1
5360 IF B9>0 OR K9>T-T0-T9 THEN GOTO LINE5400
5370 PRINT"THAT DOES IT, CAPTAIN!!  YOU ARE HEREBY RELIEVED OF COMMAND"
5380 PRINT"AND SENTENCED TO 99 STARDATES AT HARD LABOR ON CYGNUS 12!!"
5390 GOTO LINE6270

LINE5400:
5400 PRINT"STARFLEET COMMAND REVIEWING YOUR RECORD TO CONSIDER"
5410 PRINT"WALKING THE PLANK!":D0=0

LINE5430:
5430 Z1=X: Z2=Y: A$="   ":GOSUB LINE8670
5470 G(Q1,Q2)=K3*100+B3*10+S3:Z(Q1,Q2)=G(Q1,Q2):GOSUB LINE6000:GOTO LINE1990
LINE5490:
5490 PRINT"TORPEDO MISSED":GOSUB LINE6000:GOTO LINE1990


LINE5530:
REM SHIELD CONTROL
5530 IF D(7)<0 THEN PRINT"SHIELD CONTROL INOPERABLE":GOTO LINE1990
5560 PRINT"ENERGY AVAILABLE =";E+S;:INPUT"NUMBER OF UNITS TO SHIELDS";X
5580 IF X<0 OR S=X THEN PRINT"<SHIELDS UNCHANGED>":GOTO LINE1990
5590 IF X<=E+S THEN GOTO LINE5630
5600 PRINT"SHIELD CONTROL REPORTS  'THIS IS NOT THE FEDERATION TREASURY.'"
5610 PRINT"<SHIELDS UNCHANGED>":GOTO LINE1990
LINE5630:
5630 E=E+S-X:S=X:PRINT"DEFLECTOR CONTROL ROOM REPORT:"
5660 PRINT"  'SHIELDS NOW AT";INT(S);"UNITS PER YOUR COMMAND.'":GOTO LINE1990

LINE5690:
REM DAMAGE CONTROL
5690 IF D(6)>=0 THEN GOTO LINE5910
5700 PRINT"DAMAGE CONTROL REPORT NOT AVAILABLE":IFD0=0THEN GOTO LINE1990
LINE5720:
5720 D3=0:FOR I=1 TO 8:IF D(I)<0 THEN D3=D3+.1
5760 NEXTI:IFD3=0THEN GOTO LINE1990
5780 PRINT:D3=D3+D4:IF D3>=1 THEN D3=.9
5810 PRINT"TECHNICIANS STANDING BY TO EFFECT REPAIRS TO YOUR SHIP;"
5820 PRINT"ESTIMATED TIME TO REPAIR:";.01*INT(100*D3);"STARDATES"
5840 INPUT "WILL YOU AUTHORIZE THE REPAIR ORDER (Y/N)";A$
5860 IFA$<>"Y"THEN GOTO LINE1990
5870 FORI=1TO8:IFD(I)<0THEND(I)=0
5890 NEXTI:T=T+D3+.1
LINE5910:
5910 PRINT:PRINT"DEVICE             STATE OF REPAIR":FOR R1=1 TO 8
5920 GOSUB LINE8790:PRINTG2$;LEFT$(Z$,25-LEN(G2$));INT(D(R1)*100)*.01
5950 NEXT R1:PRINT:IF D0<>0 THEN GOTO LINE5720
5980 GOTO LINE1990

LINE6000:
5990 REM KLINGONS SHOOTING
6000 IF K3<=0THEN RETURN
6010 IF D0<>0THEN PRINT"STARBASE SHIELDS PROTECT THE FUMBLENUTS":RETURN
6040 FOR I=1 TO 3:IF K(I,3)<=0 THEN GOTO LINE6200
6060 H=INT((K(I,3)/FND(1))*(2+RND(1))):S=S-H:K(I,3)=K(I,3)/(3+RND(0))
6080 PRINT H;"UNIT HIT ON FUMBLENUTS FROM SECTOR";K(I,1);",";K(I,2)
6090 IF S<=0 THEN GOTO LINE6240
6100 PRINT"      <SHIELDS DOWN TO";S;"UNITS>":IF H<20 THEN GOTO LINE6200
6120 IFRND(1)>.6ORH/S<=.02THEN GOTO LINE6200
6140 R1=FNR(1):D(R1)=D(R1)-H/S-.5*RND(1):GOSUB LINE8790
6170 PRINT"DAMAGE CONTROL REPORTS ";G2$;" DAMAGED BY THE HIT'"
LINE6200:
6200 NEXT I:RETURN
6210 REM END OF GAME

LINE6220:
6220 PRINT"IT IS STARDATE";T:GOTO LINE6270
LINE6240:
6240 PRINT:PRINT"THE FUMBLENUTS HAS BEEN DESTROYED.  THEN FEDERATION ";
6250 PRINT"WILL BE CONQUERED":GOTO LINE6220

LINE6270:
6270 PRINT"THERE WERE";K9;"KLINGON BATTLE CRUISERS LEFT AT"
6280 PRINT"THE END OF YOUR MISSION."
LINE6290:
6290 PRINT:PRINT:IF B9=0 THEN GOTO LINE6360
6310 PRINT"THE FEDERATION IS IN NEED OF A NEW STARSHIP COMMANDER"
6320 PRINT"FOR A SIMILAR MISSION -- IF THERE IS A VOLUNTEER,"
6330 INPUT"LET HIM STEP FORWARD AND ENTER 'AYE'";A$:IF A$="AYE"THEN GOTO START
LINE6360:
6360 END

LINE6370:
6370 PRINT"CONGRULATION, CAPTAIN!  THEN LAST KLINGON BATTLE CRUISER"
6380 PRINT"MENACING THE FDERATION HAS BEEN DESTROYED.":PRINT
6400 PRINT"YOUR EFFICIENCY RATING IS";1000*(K7/(T-T0))^2:GOTO LINE6290

LINE6430:
REM SHORT RANGE SENSOR SCAN & STARTUP SUBROUTINE
6430 FORI=S1-1TOS1+1:FORJ=S2-1TOS2+1
6450 IFINT(I+.5)<1ORINT(I+.5)>8 OR INT(J+.5)<1 OR INT(J+.5)>8 THEN GOTO LINE6540
6490 A$=">!<":Z1=I:Z2=J:GOSUB LINE8830:IF Z3=1 THEN LINE6580
LINE6540:
6540 NEXT J:NEXT I:D0=0: GOTO LINE6650
LINE6580:
6580 D0=1:C$="DOCKED":E=E0:P=P0
6620 PRINT"SHIELDS DROPPED FOR DOCKING PURPOSES":S=0:GOTO LINE6720
LINE6650:
6650 IFK3>0THENC$="*RED*":GOTO LINE6720
6660 C$="GREEN":IF E<E0*.1 THEN C$="YELLOW"
LINE6720:
6720 IF D(2)>=0 THEN GOTO LINE6770
6730 PRINT:PRINT"*** SHORT RANGE SENSORS ARE OUT ***":PRINT:RETURN
LINE6770:
6770 O1$="---------------------------------":PRINT O1$:FOR I=1 TO 8
6820 FOR J=(I-1)*24+1TO(I-1)*24+22 STEP 3:PRINT" ";MID$(Q$,J,3);:NEXT J
6830 ON I GOTO LINE6850,LINE6900,LINE6960,LINE7020,LINE7070,LINE7120,LINE7180,LINE7240
LINE6850:
6850 PRINT"        STARDATE          ";INT(T*10)*.1:GOTO LINE7260
LINE6900:
6900 PRINT"        CONDITION          ";C$:GOTO LINE7260
LINE6960:
6960 PRINT"        QUADRANT          ";Q1;",";Q2:GOTO LINE7260
LINE7020:
7020 PRINT"        SECTOR            ";S1;",";S2:GOTO LINE7260
LINE7070:
7070 PRINT"        PHOTON TORPEDOES  ";INT(P):GOTO LINE7260
LINE7120:
7120 PRINT"        TOTAL ENERGY      ";INT(E+S):GOTO LINE7260
LINE7180:
7180 PRINT"        SHIELDS           ";INT(S):GOTO LINE7260
LINE7240:
7240 PRINT"        KLINGONS REMAINING";INT(K9)
LINE7260:
7260 NEXT I:PRINT O1$:RETURN


LINE7290:
REM LIBRARY COMPUTER CODE
7290 IFD(8)<0THENPRINT"COMPUTER DISABLED":GOTO LINE1990
7320 INPUT"COMPUTER ACTIVE AND AWAITING COMMAND";A:IFA<0THEN GOTO LINE1990
7350 PRINT:H8=1:ON A+1 GOTO LINE7540,LINE7900,LINE8070,LINE8500,LINE8150,LINE7400
7360 PRINT"FUNCTIONS AVAILABLE FROM LIBRARY-COMPUTER:"
7370 PRINT"   0 = CUMULATIVE GALACTIC RECORD"
7372 PRINT"   1 = STATUS REPORT"
7374 PRINT"   2 = PHOTON TORPEDO DATA"
7376 PRINT"   3 = STARBASE NAV DATA"
7378 PRINT"   4 = DIRECTION/DISTANCE CALCULATOR"
7380 PRINT"   5 = GALAXY 'REGION NAME' MAP":PRINT:GOTO7320
7390 REM SETUP TO CHANGE CUMULATIVE GALACTIC RECORD TO GALAXY MAP

LINE7400:
7400 H8=0:G5=1:PRINT"                        THE GALAXY":GOTO LINE7550

LINE7540:
REM CUMULATIVE GALACTIC RECORD
7540 REM INPUT"DO YOU WANT A HARDCOPY? IS THE TTY ON (Y/N)";A$
7542 REM IFA$="Y"THENPOKE1229,2:POKE1237,3:NULL1
7543 PRINT:PRINT"        ";
7544 PRINT"COMPUTER RECORD OF GALAXY FOR QUADRANT";Q1;",";Q2
7546 PRINT

LINE7550:
7550 PRINT"       1     2     3     4     5     6     7     8"
7560 O1$="     ----- ----- ----- ----- ----- ----- ----- -----"
7570 PRINT O1$:FOR I=1 TO 8:PRINT I;:IF H8=0 THEN GOTO LINE7740
7630 FORJ=1TO8:PRINT"   ";:IFZ(I,J)=0THEN PRINT"***";:GOTO LINE7720
7700 PRINT RIGHT$(STR$(Z(I,J)+1000),3);
LINE7720:
7720 NEXT J:GOTO LINE7850
LINE7740:
7740 Z4=I:Z5=1:GOSUB LINE9030:J0=INT(15-.5*LEN(G2$)):PRINT TAB(J0);G2$;
7800 Z5=5:GOSUB LINE9030:J0=INT(39-.5*LEN(G2$)):PRINT TAB(J0);G2$;
LINE7850:
7850 PRINT:PRINTO1$:NEXTI:PRINT:GOTO LINE1990

LINE7900:
7890 REM STATUS REPORT
7900 PRINT "   STATUS REPORT:":X$="":IF K9>1 THEN X$="S"
7940 PRINT"KLINGON";X$;" LEFT: ";K9
7960 PRINT"MISSION MUST BE COMPLETED IN";.1*INT((T0+T9-T)*10);"STARDATES"
7970 X$="S":IF B9<2 THEN X$="":IF B9<1 THEN GOTO LINE8010
7980 PRINT"THE FEDERATION IS MAINTAINING";B9;"STARBASE";X$;" IN THE GALAXY"
7990 GOTO LINE5690
LINE8010:
8010 PRINT"YOUR STUPIDITY HAS LEFT YOU ON YOUR ON IN"
8020 PRINT"  THE GALAXY -- YOU HAVE NO STARBASES LEFT!":GOTO LINE5690

LINE8070:
REM TORPEDO, BASE NAV, D/D CALCULATOR
8070 IF K3<=0 THEN GOTO LINE4270
8080 X$="":IFK3>1THENX$="S"
8090 PRINT"FROM FUMBLENUTS TO KLINGON BATTLE CRUSER";X$
8100 H8=0:FORI=1TO3:IFK(I,3)<=0THEN GOTO LINE8480
8110 W1=K(I,1):X=K(I,2)
LINE8120:
8120 C1=S1:A=S2:GOTO LINE8220

LINE8150:
8150 PRINT"DIRECTION/DISTANCE CALCULATOR:"
8160 PRINT"YOU ARE AT QUADRANT ";Q1;",";Q2;" SECTOR ";S1;",";S2
8170 PRINT"PLEASE ENTER":INPUT"  INITIAL COORDINATES (X,Y)";C1,A
8200 INPUT"  FINAL COORDINATES (X,Y)";W1,X

LINE8220:
8220 X=X-A:A=C1-W1:IF X<0 THEN GOTO LINE8350
8250 IF A<0 THEN GOTO LINE8410
8260 IF X>0 THEN GOTO LINE8280
8270 IF A=0 THEN C1=5:GOTO LINE8290
LINE8280:
8280 C1=1
LINE8290:
8290 IFABS(A)<=ABS(X)THEN GOTO LINE8330
8310 PRINT"DIRECTION =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO LINE8460
LINE8330:
8330 PRINT"DIRECTION =";C1+(ABS(A)/ABS(X)):GOTO LINE8460

LINE8350:
8350 IF A> 0 THEN C1=3:GOTO LINE8420
8360 IF X<> 0 THEN C1=5:GOTO LINE8290
LINE8410:
8410 C1=7
LINE8420:
8420 IF ABS(A) >= ABS(X) THEN GOTO LINE8450
8430 PRINT"DIRECTION =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X)):GOTO LINE8460
LINE8450:
8450 PRINT"DIRECTION =";C1+(ABS(X)/ABS(A))
LINE8460:
8460 PRINT"DISTANCE =";SQR(X^2+A^2):IFH8=1THEN GOTO LINE1990
LINE8480:
8480 NEXT I:GOTO LINE1990

LINE8500:
8500 IF B3<>0 THEN PRINT"FROM FUMBLENUTS TO STARBASE:":W1=B4:X=B5:GOTO LINE8120
8510 PRINT"MR. NIBBLES REPORTS,  'SENSORS SHOW NO STARBASES IN THIS";
8520 PRINT" QUADRANT.'":GOTO LINE1990
8580 REM FIND EMPTY PLACE IN QUADRANT (FOR THINGS)
LINE8590:
8590 R1=FNR(1):R2=FNR(1):A$="   ":Z1=R1:Z2=R2:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE8590
8600 RETURN



LINE8670:
REM INSERT IN STRING ARRAY FOR QUADRANT
8670 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
8675 IF LEN(A$)<>3THEN PRINT"ERROR":STOP
8680 IF S8=1 THEN Q$=A$+RIGHT$(Q$,189):RETURN
8690 IF S8=190 THEN Q$=LEFT$(Q$,189)+A$:RETURN
8700 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN

LINE8790:
REM PRINTS DEVICE NAME
8790 ON R1 GOTO LINE8792,LINE8794,LINE8796,LINE8798,LINE8800,LINE8802,LINE8804,LINE8806
LINE8792:
G2$="WARP ENGINES":RETURN
LINE8794:
G2$="SHORT RANGE SENSORS":RETURN
LINE8796:
G2$="LONG RANGE SENSORS":RETURN
LINE8798:
G2$="PHASER CONTROL":RETURN
LINE8800:
G2$="PHOTON TUBES":RETURN
LINE8802:
G2$="DAMAGE CONTROL":RETURN
LINE8804:
G2$="SHIELD CONTROL":RETURN
LINE8806:
G2$="LIBRARY-COMPUTER":RETURN

LINE8830:
REM STRING COMPARISON IN QUADRANT ARRAY
8830 Z1=INT(Z1+.5):Z2=INT(Z2+.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
8890 IFMID$(Q$,S8,3)<>A$THENRETURN
8900 Z3=1:RETURN

LINE9030:
9010 REM QUADRANT NAME IN G2$ FROM Z4,Z5 (=Q1,Q2)
9020 REM CALL WITH G5=1 TO GET REGION NAME ONLY
9030 IF Z5 <=4 THEN ON Z4 GOTO LINE9040,LINE9050,LINE9060,LINE9070,LINE9080,LINE9090,LINE9100,LINE9110
9035 GOTO LINE9120
LINE9040:
9040 G2$="ANTARES":GOTO LINE9210
LINE9050:
G2$="RIGEL":GOTO LINE9210
LINE9060:
G2$="PROCYON":GOTO LINE9210
LINE9070:
G2$="VEGA":GOTO LINE9210
LINE9080:
G2$="CANOPUS":GOTO LINE9210
LINE9090:
G2$="ALTAIR":GOTO LINE9210
LINE9100:
G2$="SAGITTARIUS":GOTO LINE9210
LINE9110:
G2$="POLLUX":GOTO LINE9210
LINE9120:
9120 ON Z4 GOTO LINE9130,LINE9140,LINE9150,LINE9160,LINE9170,LINE9180,LINE9190,LINE9200
LINE9130:
G2$="SIRIUS":GOTO LINE9210
LINE9140:
G2$="DENEB":GOTO LINE9210
LINE9150:
G2$="CAPELLA":GOTO LINE9210
LINE9160:
G2$="BETELGEUSE":GOTO LINE9210
LINE9170:
G2$="ALDEBARAN":GOTO LINE9210
LINE9180:
G2$="REGULUS":GOTO LINE9210
LINE9190:
G2$="ARCTURUS":GOTO LINE9210
LINE9200:
G2$="SPICA"
LINE9120:
9210 IF G5<>1 THEN ON Z5 GOTO LINE9230,LINE9240,LINE9250,LINE9260,LINE9230,LINE9240,LINE9250,LINE9260
9220 RETURN
LINE9230:
G2$=G2$+" I":RETURN
LINE9240:
G2$=G2$+" II":RETURN
LINE9250:
G2$=G2$+" III":RETURN
LINE9260:
G2$=G2$+" IV":RETURN






