REM ------------------------------------------------------
REM **** BASLOAD ISO CRLF 
REM
REM **** Commander X16 version of the 40+ year old classic 
REM **** Star Trek Main Frame game - SEPT 21, 2024
REM ------------------------------------------------------

REM -- ORGINAL REMARKS --
REM SUPER STARTREK - MAY 16,1978 - REQUIRES 24K MEMORY
REM ****        **** STAR TREK ****        ****
REM **** SIMULATION OF A MISSION OF THE STARSHIP ENTERPRISE,
REM **** AS SEEN ON THE STAR TREK TV SHOW.
REM **** ORIGIONAL PROGRAM BY MIKE MAYFIELD, MODIFIED VERSION
REM **** PUBLISHED IN DEC'S "101 BASIC GAMES", BY DAVE AHL.
REM **** MODIFICATIONS TO THE LATTER (PLUS DEBUGGING) BY BOB
REM *** LEEDOM - APRIL & DECEMBER 1974,
REM *** WITH A LITTLE HELP FROM HIS FRIENDS . . .
REM *** COMMENTS, EPITHETS, AND SUGGESTIONS SOLICITED --
REM *** SEND TO:  R. C. LEEDOM
REM ***           WESTINGHOUSE DEFENSE & ELECTRONICS SYSTEMS CNTR.
REM ***           BOX 746, M.S. 338
REM ***           BALTIMORE, MD  21203
REM ***
REM *** CONVERTED TO MICROSOFT 8 K BASIC 3/16/78 BY JOHN GORDERS
REM *** LINE NUMBERS FROM VERSION STREK7 OF 1/12/75 PRESERVED AS
REM *** MUCH AS POSSIBLE WHILE USING MULTIPLE STATEMENTS PER LINE
REM *** SOME LINES ARE LONGER THAN 72 CHARACTERS; THIS WAS DONE
REM *** BY USING "?" INSTEAD OF "PRINT" WHEN ENTERING LINES
REM ------------------------------------------------------


#CONTROLCODES 1
#SYMFILE "@:SWRECK-ORG.SYM"
#SAVEAS "@:SWRECK-ORG.PRG"

START:
CLS
FOR P = 1 TO 11 : PRINT : NEXT P
REM --- PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
PRINT"                                    ,------*------,"
PRINT"                    ,-------------   '---  ------'"
PRINT"                     '-------- --'      / /"
PRINT"                         ,---' '-------/ /--,"
PRINT"                          '----------------'":PRINT
PRINT"                    THE HMSS FUMBLENUTS --- NCAF-1201Q"
REM --- PRINT:PRINT:PRINT:PRINT:PRINT
FOR P = 1 TO 5 : PRINT : NEXT P
Z$="                         "
DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
T=INT(RND(1)*20+20)*100:T0=T:T9=25+INT(RND(1)*10):D0=0:E=3000:E0=E
P=10:P0=P:S9=200:S=0:B9=2:K9=0:X$="":X0$=" IS "
DEF FN D(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
DEF FN R(R)=INT(RND(R)*7.98+1.01)

REM INITIALIZE ENTERPRIZE'S POSITION
Q1=FN R(1):Q2=FN R(1):S1=FN R(1):S2=FN R(1)
FOR I=1 TO 9:C(I,1)=0:C(I,2)=0:NEXT I
C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
FOR I=1 TO 8:D(I)=0:NEXT I
A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"

REM SETUP WHAT EXHISTS IN GALAXY . . .
REM K3= # KLINGONS  B3= # STARBASES  S3 = # STARS
FOR I=1 TO 8:FOR J=1 TO 8:K3=0:Z(I,J)=0:R1=RND(1)
IF R1>.98 THEN K3=3:K9=K9+3:GOTO LINE980
IF R1>.95 THEN K3=2:K9=K9+2:GOTO LINE980
IF R1>.80 THEN K3=1:K9=K9+1
LINE980:
B3=0:IF RND(1)>.96THEN B3=1:B9=B9+1
G(I,J)=K3*100+B3*10+FN R(1):NEXT J:NEXT I:IF K9>T9 THEN T9=K9+1
IF B9<>0 THEN GOTO LINE1200
IF G(Q1,Q2)<200 THEN G(Q1,Q2)=G(Q1,Q2)+120:K9=K9+1
B9=1:G(Q1,Q2)=G(Q1,Q2)+10:Q1=FN R(1):Q2=FN R(1)
LINE1200:
K7=K9:IF B9<>1THEN X$="S":X0$=" ARE "
PRINT"YOUR ORDERS ARE AS FOLLOWS:"
PRINT"     DESTROY THE";K9;"KLINGON WARSHIPS WHICH HAVE INVADED"
PRINT"   THE GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS"
PRINT"   ON STARDATE";T0+T9;"  THIS GIVES YOU";T9;"DAYS.  THERE";X0$
PRINT"  ";B9;"STARBASE";X$;" IN THE GALAXY FOR RESUPPLYING YOUR SHIP"
PRINT:REM PRINT"HIT ANY KEY EXCEPT RETURN WHEN READY TO ACCEPT COMMAND"
LINE1300:
I=RND(1):REM IF INP(1)=13 THEN GOTO LINE1300

LINE1320:
REM HERE ANY TIME NEW QUADRANT ENTERED
Z4=Q1:Z5=Q2:K3=0:B3=0:S3=0:G5=0:D4=.5*RND(1):Z(Q1,Q2)=G(Q1,Q2)
IF Q1<1 OR Q1>8 OR Q2<1 OR Q2>8 THEN GOTO LINE1600
GOSUB LINE9030:PRINT:IF T0<>T THEN GOTO LINE1490
PRINT"YOUR MISSION BEGINS WITH YOUR STARSHIP LOCATED"
PRINT"IN THE GALACTIC QUADRANT, '";G2$;"'.":GOTO LINE1500
LINE1490:
PRINT"NOW ENTERING ";G2$;" QUADRANT . . ."

LINE1500:
PRINT:K3=INT(G(Q1,Q2)*.01):B3=INT(G(Q1,Q2)*.1)-10*K3

S3=G(Q1,Q2)-100*K3-10*B3:IF K3=0 THEN GOTO LINE1590
PRINT"COMBAT AREA      CONDITION RED":IF S>200 THEN GOTO LINE1590
PRINT"   SHIELDS DANGEROUSLY LOW"
LINE1590:
FOR I=1 TO 3:K(I,1)=0:K(I,2)=0:NEXT I

LINE1600:
FOR I=1 TO 3:K(I,3)=0:NEXT I:Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
REM POSITION FUMBLENUTS IN QUADRANT, THEN PLACE "K3" KLINGONS, &
REM "B3" STARBASES, & "S3" STARS ELSEWHERE.
A$="<*>":Z1=S1:Z2=S2:GOSUB LINE8670:IF K3<1 THEN GOTO LINE1820
FOR I=1 TO K3:GOSUB LINE8590:A$="+K+":Z1=R1:Z2=R2
GOSUB LINE8670:K(I,1)=R1:K(I,2)=R2:K(I,3)=S9*(0.5+RND(1)):NEXT I
LINE1820:
IF B3<1 THEN GOTO LINE1910
GOSUB LINE8590:A$=">!<":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB LINE8670
LINE1910:
FOR I=1 TO S3:GOSUB LINE8590:A$=" * ":Z1=R1:Z2=R2:GOSUB LINE8670:NEXT I

LINE1980:
GOSUB LINE6430

LINE1990:
IF S+E > 10 THEN IF E > 10 OR D(7) = 0 THEN GOTO LINE2060

PRINT:PRINT"** FATAL ERROR **   YOU'VE JUST STRANDED YOUR SHIP IN "
PRINT"SPACE":PRINT"YOU HAVE INSUFFICIENT MANEUVERING ENERGY,";
PRINT" AND SHIELD CONTROL":PRINT"IS PRESENTLY INCAPABLE OF CROSS";
PRINT"-CIRCUITING TO ENGINE ROOM!!":GOTO LINE6220

LINE2060:
INPUT"COMMAND";A$
FOR I=1 TO 9:IF LEFT$(A$,3) <> MID$(A1$,3*I-2,3)THEN GOTO LINE2160
ON I GOTO LINE2300,LINE1980,LINE4000,LINE4260,LINE4700,LINE5530,LINE5690,LINE7290,LINE6270

LINE2160:
NEXT I:PRINT"ENTER ONE OF THE FOLLOWING:"
PRINT"  NAV  (TO SET COURSE)"
PRINT"  SRS  (FOR SHORT RANGE SENSOR SCAN)"
PRINT"  LRS  (FOR LONG RANGE SENSOR SCAN)"
PRINT"  PHA  (TO FIRE PHASERS)"
PRINT"  TOR  (TO FIRE PHOTON TORPEDOES)"
PRINT"  SHE  (TO RAISE OR LOWER SHIELDS)"
PRINT"  DAM  (FOR DAMAGE CONTROL REPORTS)"
PRINT"  COM  (TO CALL ON LIBRARY-COMPUTER)"
PRINT"  XXX  (TO RESIGN YOUR COMMAND)":PRINT:GOTO LINE1990

LINE2300:
REM COURSE CONTROL BEGINS HERE
INPUT"COURSE (0-9)";C1:IF C1=9 THEN C1=1
IF C1>=1 AND C1<9 THEN GOTO LINE2350
PRINT"   LT. SULU REPORTS, 'INCORRECT COURSE DATA, SIR!'":GOTO LINE1990
LINE2350:
X$="8":IF D(1)<0 THEN X$="0.2"
PRINT"WARP FACTOR (0-";X$;")";:INPUT W1:IF D(1)<0 AND W1>.2 THEN GOTO LINE2470
IF W1>0 AND W1<=8 THEN GOTO LINE2490
IF W1=0 THEN GOTO LINE1990
PRINT"   CHIEF ENGINEER SNOTTY REPORTS 'THE ENGINES WON'T TAKE";
PRINT" WARP ";W1;"!'":GOTO LINE1990
LINE2470:
PRINT"WARP ENGINES ARE DAMAGED.  MAXIUM SPEED = WARP 0.2":GOTO LINE1990
LINE2490:
N=INT(W1*8+.5):IF E-N>=0 THEN GOTO LINE2590
PRINT"ENGINEERING REPORTS   'INSUFFICIENT ENERGY AVAILABLE"
PRINT"                       FOR MANEUVERING AT WARP";W1;"!'"
IF S<N-E OR D(7)<0 THEN GOTO LINE1990
PRINT"DEFLECTOR CONTROL ROOM ACKNOWLEDGES";S;"UNITS OF ENERGY"
PRINT"                         PRESENTLY DEPLOYED TO SHIELDS."
GOTO LINE1990

LINE2590:
REM KLINGONS MOVE/FIRE ON MOVING STARSHIP . . .
FOR I=1 TO K3:IF K(I,3)=0THEN 2700
A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB LINE8670:GOSUB LINE8590
K(I,1)=Z1:K(I,2)=Z2:A$="+K+":GOSUB LINE8670
NEXT I:GOSUB LINE6000:D1=0:D6=W1:IF W1>=1 THEN D6=1
FOR I=1 TO 8:IF D(I)>=0 THEN GOTO LINE2880
D(I)=D(I)+D6:IF D(I)>-.1 AND D(I)<0 THEN D(I)=-.1:GOTO LINE2880
IF D(I)<0THEN GOTO LINE2880
IF D1<>1 THEN D1=1:PRINT"DAMAGE CONTROL REPORT:  ";
PRINT TAB(8);:R1=I:GOSUB LINE8790:PRINT G2$;" REPAIR COMPLETED."

LINE2880:
NEXT I:IF RND(1)>.2THEN GOTO LINE3070
R1 = FN R(1):IF RND(1)>=.6 THEN GOTO LINE3000
D(R1)=D(R1)-(RND(1)*5+1):PRINT"DAMAGE CONTROL REPORT:  ";
GOSUB LINE8790:PRINT G2$;" DAMAGED":PRINT:GOTO LINE3070
LINE3000:
D(R1)=D(R1)+RND(1)*3+1:PRINT"DAMAGE CONTROL REPORT:  ";
GOSUB LINE8790:PRINT G2$;" STATE OF REPAIR IMPROVED":PRINT

LINE3070:
REM BEGIN MOVING STARSHIP
A$="   ":Z1=INT(S1):Z2=INT(S2):GOSUB LINE8670
X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):X=S1:Y=S2
X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):Q4=Q1:Q5=Q2
FOR I=1 TO N:S1=S1+X1:S2=S2+X2:IF S1<1 OR S1>=9 OR S2<1 OR S2>=9THEN GOTO LINE3500
S8=INT(S1)*24 + INT(S2)*3-26:IF MID$(Q$,S8,2)="  " THEN GOTO LINE3360
S1=INT(S1-X1):S2=INT(S2-X2):PRINT"WARP ENGINES SHUT DOWN AT ";
PRINT"SECTOR";S1;",";S2;"DUE TO BAD NAVAGATION":GOTO LINE3370
LINE3360:
NEXT I:S1=INT(S1):S2=INT(S2)

LINE3370:
A$="<*>":Z1=INT(S1):Z2=INT(S2):GOSUB LINE8670:GOSUB LINE3910:T8=1
IF W1<1 THEN T8=.1*INT(10*W1)
T=T+T8:IF T>T0+T9 THEN GOTO LINE6220
REM SEE IF DOCKED, THEN GET COMMAND
GOTO LINE1980

LINE3500:
REM EXCEEDED QUADRANT LIMITS
X=8*Q1+X+N*X1:Y=8*Q2+Y+N*X2:Q1=INT(X/8):Q2=INT(Y/8):S1=INT(X-Q1*8)
S2=INT(Y-Q2*8):IF S1=0 THEN Q1=Q1-1:S1=8
IF S2=0 THEN Q2=Q2-1:S2=8
X5=0:IF Q1<1 THEN X5=1:Q1=1:S1=1
IF Q1>8 THEN X5=1:Q1=8:S1=8
IF Q2<1 THEN X5=1:Q2=1:S2=1
IF Q2>8 THEN X5=1:Q2=8:S2=8
IF X5=0 THEN GOTO LINE3860
PRINT"LT. UHURA REPORTS MESSAGE FROM STARFLEET COMMAND:"
PRINT"  'PERMISSION TO ATTEMPT CROSSING OF GALACTIC PERIMETER"
PRINT"  IS HEREBY *DENIED*.  SHUT DOWN YOUR ENGINES.'"
PRINT"CHIEF ENGINEER SCOTT REPORTS  'WARP ENGINES SHUT DOWN"
PRINT"  AT SECTOR";S1;",";S2;"OF QUADRANT";Q1;",";Q2;".'"
IF T>T0+T9 THEN GOTO LINE6220
LINE3860:
IF 8*Q1+Q2=8*Q4+Q5 THEN GOTO LINE3370
T=T+1:GOSUB LINE3910:GOTO LINE1320
REM MANEUVER ENERGY S/R **
LINE3910:
E=E-N-10:IF E>=0 THEN RETURN
PRINT"SHIELD CONTROL SUPPLIES ENERGY TO COMPLETE THE MANEUVER."
S=S+E:E=0:IF S<=0 THEN S=0
RETURN


LINE4000:
REM LONG RANGE SENSOR SCAN CODE
IF D(3)<0 THEN PRINT"LONG RANGE SENSORS ARE INOPERABLE":GOTO LINE1990
PRINT"LONG RANGE SCAN FOR QUADRANT";Q1;",";Q2
O1$ = "-------------------":PRINT O1$
FOR I=Q1-1 TO Q1+1:N(1)=-1:N(2)=-2:N(3)=-3:FOR J=Q2-1 TO Q2+1
IF I>0 AND I<9 AND J>0 AND J<9 THEN N(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
NEXT J:FOR L=1 TO 3:PRINT": ";:IF N(L)<0 THEN PRINT"*** ";:GOTO LINE4230
PRINT RIGHT$(STR$(N(L)+1000),3);" ";
LINE4230:
NEXT L:PRINT":":PRINT O1$:NEXT I:GOTO LINE1990

LINE4260:
REM PHASER CONTROL CODE BEGINS HERE
IF D(4)<0 THEN PRINT "PHASERS INOPERATIVE":GOTO LINE1990
IF K3>0 THEN LINE4330
LINE4270:
PRINT"SCIENCE OFFICER SPOCK REPORTS  'SENSORS SHOW NO ENEMY SHIPS"
PRINT"                                IN THIS QUADRANT'":GOTO LINE1990
LINE4330:
IF D(8)<0 THEN PRINT"COMPUTER FAILURE HAMPERS ACCURACY"
PRINT"PHASERS LOCKED ON TARGET;  ";
LINE4360:
PRINT"ENERGY AVAILABLE =";E;"UNITS"
INPUT"NUMBER OF UNITS TO FIRE";X:IF X<=0 THEN GOTO LINE1990
IF E-X<0 THEN GOTO LINE4360
E=E-X:IF D(7)<0 THEN X=X*RND(1)
H1=INT(X/K3):FOR I=1 TO 3:IF K(I,3)<=0THEN GOTO LINE4670
H=INT((H1/FN D(0))*(RND(1)+2)):IF H>.15*K(I,3) THEN GOTO LINE4530
PRINT"SENSORS SHOW NO DAMAGE TO ENEMY AT ";K(I,1);",";K(I,2):GOTO LINE4670
LINE4530:
K(I,3)=K(I,3)-H:PRINT H;"UNIT HIT ON KLINGON AT SECTOR";K(I,1);",";
PRINT K(I,2):IF K(I,3)<=0THEN PRINT"*** KLINGON DESTROYED ***":GOTO LINE4580
PRINT"   (SENSORS SHOW";K(I,3);"UNITS REMAINING)":GOTO LINE4670
LINE4580:
K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB LINE8670
K(I,3)=0:G(Q1,Q2)=G(Q1,Q2)-100:Z(Q1,Q2)=G(Q1,Q2):IF K9<=0 THEN GOTO LINE6370
LINE4670:
NEXT I:GOSUB LINE6000:GOTO LINE1990

LINE4700:
REM PHOTON TORPEDO CODE BEGINS HERE
IF P<=0 THEN PRINT"ALL PHOTON TORPEDOES EXPENDED":GOTO LINE1990
IF D(5)<0THEN PRINT"PHOTON TUBES ARE NOT OPERATIONAL":GOTO LINE1990
LINE4760:
INPUT"PHOTON TORPEDO COURSE (1-9)";C1:IF C1=9 THEN C1=1
IF C1>=1 AND C1<9 THEN GOTO LINE4850
PRINT"ENSIGN CHEKOV REPORTS,  'INCORRECT COURSE DATA, SIR!'"
GOTO LINE1990
LINE4850:
X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):E=E-2:P=P-1
X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=S1:Y=S2
PRINT"TORPEDO TRACK:"
LINE4920:
X=X+X1:Y=Y+X2:X3=INT(X+.5):Y3=INT(Y+.5)
IF X3<1 OR X3>8 OR Y3<1 OR Y3>8 THEN GOTO LINE5490
PRINT"               ";X3;",";Y3:A$="   ":Z1=X:Z2=Y:GOSUB LINE8830
IF Z3<>0 THEN GOTO LINE4920
A$="+K+":Z1=X:Z2=Y:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE5210
PRINT"*** KLINGON DESTROYED ***":K3=K3-1:K9=K9-1:IF K9<=0 THEN GOTO LINE6370
FOR I=1 TO 3:IF X3=K(I,1) AND Y3=K(I,2)THEN GOTO LINE5190
NEXT I:I=3
LINE5190:
5190 K(I,3)=0:GOTO LINE5430
LINE5210:
A$=" * ":Z1=X:Z2=Y:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE5280
PRINT "STAR AT";X3;",";Y3;"ABSORBED TORPEDO ENERGY.":GOSUB LINE6000:GOTO LINE1990
LINE5280:
A$=">!<":Z1=X:Z2=Y:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE4760
PRINT"*** STARBASE DESTROYED ***":B3=B3-1:B9=B9-1
IF B9>0 OR K9>T-T0-T9 THEN GOTO LINE5400
PRINT"THAT DOES IT, CAPTAIN!!  YOU ARE HEREBY RELIEVED OF COMMAND"
PRINT"AND SENTENCED TO 99 STARDATES AT HARD LABOR ON CYGNUS 12!!"
GOTO LINE6270

LINE5400:
PRINT"STARFLEET COMMAND REVIEWING YOUR RECORD TO CONSIDER"
PRINT"WALKING THE PLANK!":D0=0

LINE5430:
Z1=X: Z2=Y: A$="   ":GOSUB LINE8670
G(Q1,Q2)=K3*100+B3*10+S3:Z(Q1,Q2)=G(Q1,Q2):GOSUB LINE6000:GOTO LINE1990
LINE5490:
PRINT"TORPEDO MISSED":GOSUB LINE6000:GOTO LINE1990


LINE5530:
REM SHIELD CONTROL
IF D(7)<0 THEN PRINT"SHIELD CONTROL INOPERABLE":GOTO LINE1990
PRINT"ENERGY AVAILABLE =";E+S;:INPUT"NUMBER OF UNITS TO SHIELDS";X
IF X<0 OR S=X THEN PRINT"<SHIELDS UNCHANGED>":GOTO LINE1990
IF X<=E+S THEN GOTO LINE5630
PRINT"SHIELD CONTROL REPORTS  'THIS IS NOT THE FEDERATION TREASURY.'"
PRINT"<SHIELDS UNCHANGED>":GOTO LINE1990
LINE5630:
E=E+S-X:S=X:PRINT"DEFLECTOR CONTROL ROOM REPORT:"
PRINT"  'SHIELDS NOW AT";INT(S);"UNITS PER YOUR COMMAND.'":GOTO LINE1990

LINE5690:
REM DAMAGE CONTROL
IF D(6)>=0 THEN GOTO LINE5910
PRINT"DAMAGE CONTROL REPORT NOT AVAILABLE":IF D0=0 THEN GOTO LINE1990
LINE5720:
D3=0:FOR I=1 TO 8:IF D(I)<0 THEN D3=D3+.1
NEXT I:IF D3=0 THEN GOTO LINE1990
PRINT:D3=D3+D4:IF D3>=1 THEN D3=.9
PRINT"TECHNICIANS STANDING BY TO EFFECT REPAIRS TO YOUR SHIP;"
PRINT"ESTIMATED TIME TO REPAIR:";.01*INT(100*D3);"STARDATES"
INPUT "WILL YOU AUTHORIZE THE REPAIR ORDER (Y/N)";A$
IF A$<>"Y" THEN GOTO LINE1990
FOR I=1 TO 8:IF D(I)<0 THEN D(I)=0
NEXT I:T=T+D3+.1
LINE5910:
PRINT:PRINT"DEVICE             STATE OF REPAIR":FOR R1=1 TO 8
GOSUB LINE8790:PRINT G2$;LEFT$(Z$,25-LEN(G2$));INT(D(R1)*100)*.01
NEXT R1:PRINT:IF D0<>0 THEN GOTO LINE5720
GOTO LINE1990

LINE6000:
REM KLINGONS SHOOTING
IF K3 <=0 THEN RETURN
IF D0 <>0 THEN PRINT"STARBASE SHIELDS PROTECT THE FUMBLENUTS":RETURN
FOR I=1 TO 3:IF K(I,3)<=0 THEN GOTO LINE6200
H=INT((K(I,3)/FN D(1))*(2+RND(1))):S=S-H:K(I,3)=K(I,3)/(3+RND(0))
PRINT H;"UNIT HIT ON FUMBLENUTS FROM SECTOR";K(I,1);",";K(I,2)
IF S<=0 THEN GOTO LINE6240
PRINT"      <SHIELDS DOWN TO";S;"UNITS>":IF H<20 THEN GOTO LINE6200
IF RND(1) >. 6 OR H/S <= .02 THEN GOTO LINE6200
R1 = FN R(1):D(R1) = D(R1) - H/S-.5 * RND(1):GOSUB LINE8790
PRINT"DAMAGE CONTROL REPORTS ";G2$;" DAMAGED BY THE HIT'"
LINE6200:
NEXT I:RETURN
REM END OF GAME

LINE6220:
PRINT"IT IS STARDATE";T:GOTO LINE6270
LINE6240:
PRINT:PRINT"THE FUMBLENUTS HAS BEEN DESTROYED.  THEN FEDERATION ";
PRINT"WILL BE CONQUERED":GOTO LINE6220

LINE6270:
PRINT"THERE WERE";K9;"KLINGON BATTLE CRUISERS LEFT AT"
PRINT"THE END OF YOUR MISSION."
LINE6290:
PRINT:PRINT:IF B9=0 THEN GOTO LINE6360
PRINT"THE FEDERATION IS IN NEED OF A NEW STARSHIP COMMANDER"
PRINT"FOR A SIMILAR MISSION -- IF THERE IS A VOLUNTEER,"
INPUT"LET HIM STEP FORWARD AND ENTER 'AYE'";A$:IF A$="AYE"THEN GOTO START
LINE6360:
END

LINE6370:
PRINT"CONGRULATION, CAPTAIN!  THEN LAST KLINGON BATTLE CRUISER"
PRINT"MENACING THE FDERATION HAS BEEN DESTROYED.":PRINT
PRINT"YOUR EFFICIENCY RATING IS";1000*(K7/(T-T0))^2:GOTO LINE6290

LINE6430:
REM SHORT RANGE SENSOR SCAN & STARTUP SUBROUTINE
FOR I=S1-1 TO S1+1:FOR J=S2-1 TO S2+1
IF INT(I+.5)<1 OR INT(I+.5)>8 OR INT(J+.5)<1 OR INT(J+.5)>8 THEN GOTO LINE6540
A$=">!<":Z1=I:Z2=J:GOSUB LINE8830:IF Z3=1 THEN LINE6580
LINE6540:
NEXT J:NEXT I:D0=0: GOTO LINE6650
LINE6580:
D0=1:C$="DOCKED":E=E0:P=P0
PRINT"SHIELDS DROPPED FOR DOCKING PURPOSES":S=0:GOTO LINE6720
LINE6650:
IF K3>0 THEN C$="*RED*":GOTO LINE6720
C$="GREEN":IF E<E0*.1 THEN C$="YELLOW"
LINE6720:
IF D(2)>=0 THEN GOTO LINE6770
PRINT:PRINT"*** SHORT RANGE SENSORS ARE OUT ***":PRINT:RETURN
LINE6770:
O1$="---------------------------------":PRINT O1$:FOR I=1 TO 8
FOR J=(I-1)*24+1TO(I-1)*24+22 STEP 3:PRINT" ";MID$(Q$,J,3);:NEXT J
ON I GOTO LINE6850,LINE6900,LINE6960,LINE7020,LINE7070,LINE7120,LINE7180,LINE7240
LINE6850:
PRINT"        STARDATE          ";INT(T*10)*.1:GOTO LINE7260
LINE6900:
PRINT"        CONDITION          ";C$:GOTO LINE7260
LINE6960:
PRINT"        QUADRANT          ";Q1;",";Q2:GOTO LINE7260
LINE7020:
PRINT"        SECTOR            ";S1;",";S2:GOTO LINE7260
LINE7070:
PRINT"        PHOTON TORPEDOES  ";INT(P):GOTO LINE7260
LINE7120:
PRINT"        TOTAL ENERGY      ";INT(E+S):GOTO LINE7260
LINE7180:
PRINT"        SHIELDS           ";INT(S):GOTO LINE7260
LINE7240:
PRINT"        KLINGONS REMAINING";INT(K9)
LINE7260:
NEXT I:PRINT O1$:RETURN


LINE7290:
REM LIBRARY COMPUTER CODE
IF D(8)<0THEN PRINT"COMPUTER DISABLED":GOTO LINE1990
LINE7320:
INPUT"COMPUTER ACTIVE AND AWAITING COMMAND";A:IF A<0 THEN GOTO LINE1990
PRINT:H8=1:ON A+1 GOTO LINE7540,LINE7900,LINE8070,LINE8500,LINE8150,LINE7400
PRINT"FUNCTIONS AVAILABLE FROM LIBRARY-COMPUTER:"
PRINT"   0 = CUMULATIVE GALACTIC RECORD"
PRINT"   1 = STATUS REPORT"
PRINT"   2 = PHOTON TORPEDO DATA"
PRINT"   3 = STARBASE NAV DATA"
PRINT"   4 = DIRECTION/DISTANCE CALCULATOR"
PRINT"   5 = GALAXY 'REGION NAME' MAP":PRINT:GOTO LINE7320
REM SETUP TO CHANGE CUMULATIVE GALACTIC RECORD TO GALAXY MAP

LINE7400:
H8=0:G5=1:PRINT"                        THE GALAXY":GOTO LINE7550

LINE7540:
REM CUMULATIVE GALACTIC RECORD
REM INPUT"DO YOU WANT A HARDCOPY? IS THE TTY ON (Y/N)";A$
REM IFA$="Y"THENPOKE1229,2:POKE1237,3:NULL1
PRINT:PRINT"        ";
PRINT"COMPUTER RECORD OF GALAXY FOR QUADRANT";Q1;",";Q2
PRINT

LINE7550:
PRINT"       1     2     3     4     5     6     7     8"
O1$="     ----- ----- ----- ----- ----- ----- ----- -----"
PRINT O1$:FOR I=1 TO 8:PRINT I;:IF H8=0 THEN GOTO LINE7740
FOR J=1 TO 8:PRINT"   ";:IF Z(I,J)=0THEN PRINT"***";:GOTO LINE7720
PRINT RIGHT$(STR$(Z(I,J)+1000),3);
LINE7720:
NEXT J:GOTO LINE7850
LINE7740:
Z4=I:Z5=1:GOSUB LINE9030:J0=INT(15-.5*LEN(G2$)):PRINT TAB(J0);G2$;
Z5=5:GOSUB LINE9030:J0=INT(39-.5*LEN(G2$)):PRINT TAB(J0);G2$;
LINE7850:
PRINT:PRINT O1$:NEXT I:PRINT:GOTO LINE1990

LINE7900:
REM STATUS REPORT
PRINT "   STATUS REPORT:":X$="":IF K9>1 THEN X$="S"
PRINT"KLINGON";X$;" LEFT: ";K9
PRINT"MISSION MUST BE COMPLETED IN";.1*INT((T0+T9-T)*10);"STARDATES"
X$="S":IF B9<2 THEN X$="":IF B9<1 THEN GOTO LINE8010
PRINT"THE FEDERATION IS MAINTAINING";B9;"STARBASE";X$;" IN THE GALAXY"
GOTO LINE5690
LINE8010:
PRINT"YOUR STUPIDITY HAS LEFT YOU ON YOUR ON IN"
PRINT"  THE GALAXY -- YOU HAVE NO STARBASES LEFT!":GOTO LINE5690

LINE8070:
REM TORPEDO, BASE NAV, D/D CALCULATOR
IF K3 <=0 THEN GOTO LINE4270
X$="":IF K3>1 THEN X$="S"
PRINT"FROM FUMBLENUTS TO KLINGON BATTLE CRUSER";X$
H8=0:FOR I=1 TO 3:IF K(I,3)<=0 THEN GOTO LINE8480
W1=K(I,1):X=K(I,2)
LINE8120:
C1=S1:A=S2:GOTO LINE8220

LINE8150:
PRINT"DIRECTION/DISTANCE CALCULATOR:"
PRINT"YOU ARE AT QUADRANT ";Q1;",";Q2;" SECTOR ";S1;",";S2
PRINT"PLEASE ENTER":INPUT"  INITIAL COORDINATES (X,Y)";C1,A
INPUT"  FINAL COORDINATES (X,Y)";W1,X

LINE8220:
X=X-A:A=C1-W1:IF X<0 THEN GOTO LINE8350
IF A<0 THEN GOTO LINE8410
IF X>0 THEN GOTO LINE8280
IF A=0 THEN C1=5:GOTO LINE8290
LINE8280:
C1=1
LINE8290:
IF ABS(A)<=ABS(X)THEN GOTO LINE8330
PRINT"DIRECTION =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO LINE8460
LINE8330:
PRINT"DIRECTION =";C1+(ABS(A)/ABS(X)):GOTO LINE8460

LINE8350:
IF A > 0 THEN C1=3:GOTO LINE8420
IF X<> 0 THEN C1=5:GOTO LINE8290
LINE8410:
C1=7
LINE8420:
IF ABS(A) >= ABS(X) THEN GOTO LINE8450
PRINT"DIRECTION =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X)):GOTO LINE8460
LINE8450:
PRINT"DIRECTION =";C1+(ABS(X)/ABS(A))
LINE8460:
PRINT"DISTANCE =";SQR(X^2+A^2):IF H8=1 THEN GOTO LINE1990
LINE8480:
NEXT I:GOTO LINE1990

LINE8500:
IF B3<>0 THEN PRINT"FROM FUMBLENUTS TO STARBASE:":W1=B4:X=B5:GOTO LINE8120
PRINT"MR. NIBBLES REPORTS,  'SENSORS SHOW NO STARBASES IN THIS";
PRINT" QUADRANT.'":GOTO LINE1990
REM FIND EMPTY PLACE IN QUADRANT (FOR THINGS)
LINE8590:
R1=FN R(1):R2=FN R(1):A$="   ":Z1=R1:Z2=R2:GOSUB LINE8830:IF Z3=0 THEN GOTO LINE8590
RETURN



LINE8670:
REM INSERT IN STRING ARRAY FOR QUADRANT
S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
IF LEN(A$)<>3THEN PRINT"ERROR":STOP
IF S8=1 THEN Q$=A$+RIGHT$(Q$,189):RETURN
IF S8=190 THEN Q$=LEFT$(Q$,189)+A$:RETURN
Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN

LINE8790:
REM PRINTS DEVICE NAME
ON R1 GOTO LINE8792,LINE8794,LINE8796,LINE8798,LINE8800,LINE8802,LINE8804,LINE8806
LINE8792:
G2$="WARP ENGINES":RETURN
LINE8794:
G2$="SHORT RANGE SENSORS":RETURN
LINE8796:
G2$="LONG RANGE SENSORS":RETURN
LINE8798:
G2$="PHASER CONTROL":RETURN
LINE8800:
G2$="PHOTON TUBES":RETURN
LINE8802:
G2$="DAMAGE CONTROL":RETURN
LINE8804:
G2$="SHIELD CONTROL":RETURN
LINE8806: 
G2$="LIBRARY-COMPUTER":RETURN

LINE8830:
REM STRING COMPARISON IN QUADRANT ARRAY
Z1=INT(Z1+.5):Z2=INT(Z2+.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
IF MID$(Q$,S8,3)<>A$ THEN RETURN
Z3=1:RETURN

LINE9030:
REM QUADRANT NAME IN G2$ FROM Z4,Z5 (=Q1,Q2)
REM CALL WITH G5=1 TO GET REGION NAME ONLY
IF Z5 <=4 THEN ON Z4 GOTO LINE9040,LINE9050,LINE9060,LINE9070,LINE9080,LINE9090,LINE9100,LINE9110
REM ON Z4 GOTO LINE9040,LINE9050,LINE9060,LINE9070,LINE9080,LINE9090,LINE9100,LINE9110
GOTO LINE9120
LINE9040:
G2$="ANTARES":GOTO LINE9210
LINE9050:
G2$="RIGEL":GOTO LINE9210
LINE9060:
G2$="PROCYON":GOTO LINE9210
LINE9070:
G2$="VEGA":GOTO LINE9210
LINE9080:
G2$="CANOPUS":GOTO LINE9210
LINE9090:
G2$="ALTAIR":GOTO LINE9210
LINE9100:
G2$="SAGITTARIUS":GOTO LINE9210
LINE9110:
G2$="POLLUX":GOTO LINE9210
LINE9120:
ON Z4 GOTO LINE9130,LINE9140,LINE9150,LINE9160,LINE9170,LINE9180,LINE9190,LINE9200
LINE9130:
G2$="SIRIUS":GOTO LINE9210
LINE9140:
G2$="DENEB":GOTO LINE9210
LINE9150:
G2$="CAPELLA":GOTO LINE9210
LINE9160:
G2$="BETELGEUSE":GOTO LINE9210
LINE9170:
G2$="ALDEBARAN":GOTO LINE9210
LINE9180:
G2$="REGULUS":GOTO LINE9210
LINE9190:
G2$="ARCTURUS":GOTO LINE9210
LINE9200:
G2$="SPICA"
LINE9210:
IF G5<>1 THEN ON Z5 GOTO LINE9230,LINE9240,LINE9250,LINE9260,LINE9230,LINE9240,LINE9250,LINE9260
RETURN
LINE9230:
G2$=G2$+" I":RETURN
LINE9240:
G2$=G2$+" II":RETURN
LINE9250:
G2$=G2$+" III":RETURN
LINE9260:
G2$=G2$+" IV":RETURN






